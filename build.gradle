plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev.legacyforge' version '2.0.112'
    id 'eclipse'
    id 'idea'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
    maven {
        name = 'Bawnorton'
        url = 'https://maven.bawnorton.com/releases/'
    }
    maven {
        name = 'Sinytra'
        url = 'https://maven.su5ed.dev/releases/'
    }
    maven {
        name = 'tterrag'
        url = 'https://maven.tterrag.com/'
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'Modrinth'
                url = 'https://api.modrinth.com/maven/'
            }
        }
        filter {
            includeGroup 'maven.modrinth'
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'CurseForge'
                url = 'https://cursemaven.com/'
            }
        }
        filter {
            includeGroup 'curse.maven'
        }
    }
}

base {
    archivesName = mod_id
}

// Mojang ships Java 17 to end users from 1.18 to 1.20.4, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

legacyForge {
    version = project.forge_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    validateAccessTransformers = true

    runs {
        client {
            client()
        }
        data {
            data()
        }
        server {
            server()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

mixin {
    add sourceSets.main, 'mixins.unipack.refmap.json'
    config 'mixins.unipack.json'
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    compileOnly(annotationProcessor('com.github.bawnorton.mixinsquared:mixinsquared-common:0.2.0'))
    implementation(jarJar('com.github.bawnorton.mixinsquared:mixinsquared-forge:0.2.0'))
    compileOnly(annotationProcessor('io.github.llamalad7:mixinextras-common:0.4.1'))
    implementation(jarJar('io.github.llamalad7:mixinextras-forge:0.4.1'))
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    modImplementation 'dev.su5ed.sinytra:fabric-loader:2.7.10+0.16.5+1.20.1'

    modCompileOnly 'maven.modrinth:biome-makeover:forge-1.20.1-1.11.4'
    modCompileOnly 'maven.modrinth:blood-magic:3.3.5-47'
    modCompileOnly 'curse.maven:caupona-656147:7112017'
    modCompileOnly 'maven.modrinth:caverns-and-chasms:2.0.0'
    modCompileOnly 'curse.maven:corail-backpack-433316:5264214'
    modCompileOnly 'maven.modrinth:create:1.20.1-0.5.1.j'
    modCompileOnly 'maven.modrinth:create-tfmg:0.9.3-1.20.1'
    modCompileOnly 'maven.modrinth:destroy:1.20.1-0.1.3'
    modCompileOnly 'maven.modrinth:enigmatic-legacy:2.30.1'
    modCompileOnly 'maven.modrinth:hex-casting:O6xIFHAg'
    modCompileOnly 'maven.modrinth:jade:11.13.2+forge'
    modCompileOnly 'maven.modrinth:petrolpark:1.20.1-1.4.2'
    modCompileOnly 'maven.modrinth:projectile-damage-attribute:3.2.3+1.20.1-forge'
    modCompileOnly 'maven.modrinth:shrink:1.4.5'

    compileOnly 'maven.modrinth:complex-hex:0.1.3-beta'
    compileOnly 'maven.modrinth:geckolib:AXhbVyuq'
    compileOnly 'maven.modrinth:spectrum:1.8.11'
    compileOnly 'maven.modrinth:your-reputation:0.2.5+jade.1.20'
    // modCompileOnly doesn't work on this for some reason?
    compileOnly files('libs/geckolib-fabric-compat-1.11.2+1.20.1.jar')

    modCompileOnly 'maven.modrinth:architectury-api:9.2.14+forge'
    modCompileOnly 'maven.modrinth:curios:5.14.1+1.20.1'
    modCompileOnly 'com.tterrag.registrate:Registrate:MC1.20-1.3.3'
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            forge_version          : forge_version,
            forge_version_range    : forge_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
sourceSets.main.resources.srcDir generateModMetadata
legacyForge.ideSyncTask generateModMetadata

jar {
    manifest.attributes([
            "MixinConfigs": "mixins.unipack.json"
    ])
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
